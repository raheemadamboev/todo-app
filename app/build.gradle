plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.devtools.ksp' version '1.6.21-1.0.6'
    id 'com.google.firebase.crashlytics'
}

kotlin {
    sourceSets {
        debug {
            kotlin.srcDir("build/generated/ksp/debug/kotlin")
        }
        release {
            kotlin.srcDir("build/generated/ksp/release/kotlin")
        }
    }
}

android {
    compileSdkVersion 32
    ndkVersion "21.3.6528147"

    defaultConfig {
        applicationId "xyz.teamgravity.todo"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 3
        versionName "1.1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }

        ndk {
            debugSymbolLevel 'FULL'
        }

        bundle {
            language {
                enableSplit = false
            }
        }

        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    // core
    implementation 'androidx.core:core-ktx:1.8.0'

    // compose
    implementation "androidx.compose.ui:ui:$compose_version"

    // compose material3
    implementation 'androidx.compose.material3:material3:1.0.0-alpha13'

    // compose preview
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"

    // compose activity
    implementation 'androidx.activity:activity-compose:1.4.0'

    // compose constraint layout
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.1'

    // compose icons
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    // compose viewmodel
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1"

    // compose hilt navigation
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // compose destinations
    implementation 'io.github.raamcosta.compose-destinations:core:1.6.12-beta'
    ksp 'io.github.raamcosta.compose-destinations:ksp:1.6.12-beta'

    // lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'

    // firebase
    implementation platform('com.google.firebase:firebase-bom:30.1.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-analytics'

    // room
    implementation "androidx.room:room-runtime:2.4.2"
    implementation "androidx.room:room-ktx:2.4.2"
    kapt "androidx.room:room-compiler:2.4.2"

    // coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.3'

    // hilt
    implementation 'com.google.dagger:hilt-android:2.42'
    kapt 'com.google.dagger:hilt-compiler:2.42'

    // datastore preferences
    implementation "androidx.datastore:datastore-preferences:1.0.0"
}

kapt {
    correctErrorTypes true
}

allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs += [
                    "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api"
            ]
        }
    }
}