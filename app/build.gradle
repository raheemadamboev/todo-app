plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.devtools.ksp' version '1.7.0-1.0.6'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "xyz.teamgravity.todo"
        minSdk 21
        targetSdk 33
        versionCode 5
        versionName "1.1.3"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }

        ndk {
            debugSymbolLevel 'FULL'
        }

        bundle {
            language {
                enableSplit = false
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.2.0"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }
}

dependencies {

    // core
    implementation 'androidx.core:core-ktx:1.8.0'

    // compose
    implementation "androidx.compose.ui:ui:1.3.0-beta01"

    // compose material2
    implementation "androidx.compose.material:material:1.3.0-beta01"

    // compose material3
    implementation 'androidx.compose.material3:material3:1.0.0-beta01'

    // compose preview
    implementation "androidx.compose.ui:ui-tooling-preview:1.3.0-beta01"

    // compose activity
    implementation 'androidx.activity:activity-compose:1.5.1'

    // compose constraint layout
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.1'

    // compose icons
    implementation "androidx.compose.material:material-icons-extended:1.3.0-beta01"

    // compose viewmodel
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1"

    // compose hilt navigation
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // compose destinations
    implementation 'io.github.raamcosta.compose-destinations:core:1.7.19-beta'
    ksp 'io.github.raamcosta.compose-destinations:ksp:1.7.19-beta'

    // lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    // firebase
    implementation platform('com.google.firebase:firebase-bom:30.4.1')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-analytics'

    // room
    implementation "androidx.room:room-runtime:2.4.3"
    implementation "androidx.room:room-ktx:2.4.3"
    kapt "androidx.room:room-compiler:2.4.3"

    // coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // hilt
    implementation 'com.google.dagger:hilt-android:2.43.2'
    kapt 'com.google.dagger:hilt-compiler:2.43.2'

    // datastore preferences
    implementation "androidx.datastore:datastore-preferences:1.0.0"
}

kapt {
    correctErrorTypes true
}

allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs += [
                    "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api",
                    "-opt-in=androidx.compose.material.ExperimentalMaterialApi"
            ]
        }
    }
}